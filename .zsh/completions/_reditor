#compdef reditor
# ------------------------------------------------------------------------------
# Description
# -----------
#
#  Completion script for Reditor (https://github.com/hibariya/reditor).
#
# ------------------------------------------------------------------------------
# Authors
# -------
#
#  * Kazuya Takeshima (https://github.com/mitukiii)
#
# ------------------------------------------------------------------------------

_reditor() {
    if (( CURRENT > 2 )); then
        (( CURRENT-- ))
        shift words
        _call_function 1 _reditor_${words[1]}
    else
        _arguments \
            ': : _alternative ":command:__reditor_commands" ":gem:__reditor_gems" ":lib:__reditor_libs"'
    fi
}

__reditor_commands() {
    _reditor_commands=(
        'command'
        'open[Open the library]'
        'sh[Open a shell and move to the library]'
        'version[Show version]'
    )

    _values $_reditor_commands
}

__reditor_gems() {
    _reditor_gems=($(gem list | awk '{ print $1 }'))

    _describe -t reditor_gems 'gem' _reditor_gems
}

__reditor_libs() {
    _reditor_libs=($(ruby -e 'puts Dir[*$:.map { |p| p + "/*.rb" }].map { |p| File.basename(p).sub(/\.rb$/, "") }.join("\n")'))

    _describe -t reditor_libs 'lib' _reditor_libs
}

_reditor_sh() {
    _arguments \
        ': : _alternative ":gem:__reditor_gems" ":lib:__reditor_libs"'
}

_reditor_open() {
    _arguments \
        ': : _alternative ":gem:__reditor_gems" ":lib:__reditor_libs"'
}
