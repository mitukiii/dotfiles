#compdef agharta

_agharta() {
    if (( CURRENT > 2 )); then
        (( CURRENT-- ))
        shift words
        _call_function 1 $(echo _agharta_${words[1]} | sed 's/:/_/')
    else
        _arguments \
            ': : _alternative ":task:__agharta_tasks" ":recipe:__agharta_user_recipes" ":example recipe:__agharta_example_recipes"'
    fi
}

__agharta_tasks() {
   _agharta_tasks=(
       'task'
       'user\:add[add new user]'
       'user\:default[change default user]'
       'user\:list[show user list]'
       'console[start console]'
       'edit[open or edit recipe]'
       'cat[cat recipe]'
       'execute[execute recipe]'
   )

   _values $_agharta_tasks
}

__agharta_user_recipes() {
    if (( $#AGHARTA_HOME > 0 )); then
        _agharta_home=$AGHARTA_HOME
    else
        _agharta_home=$HOME/.agharta
    fi

    if [ -d $_agharta_home/recipes ]; then
        _agharta_user_recipes=($(ls -1 $_agharta_home/recipes))
    else
        _agharta_user_recipes=()
    fi

    _describe -t agharta_user_recipe 'recipe' _agharta_user_recipes
}

__agharta_example_recipes() {
    _agharta_example_recipes=(filter.rb sample.rb stream.rb)

    _describe -t agharta_example_recipe 'example recipe' _agharta_example_recipes -P 'example/'
}

__agharta_users() {
    _agharta_users=($(agharta user:list | grep -v -E '(\(\d+ users\)|No users)' | sed 's/\*//g'))

    _describe -t agharta_users 'user' _agharta_users
}

_agharta_user_default() {
    _arguments \
        ':user:__agharta_users'
}

_agharta_edit() {
    _arguments \
        ':recipe:__agharta_user_recipes'
}

_agharta_cat() {
    _arguments \
        ': : _alternative ":recipe:__agharta_user_recipes" ":example recipe:__agharta_example_recipes"'
}

_agharta_execute() {
    _arguments \
        ': : _alternative ":recipe:__agharta_user_recipes" ":example recipe:__agharta_example_recipes"'
}
